plugins {
	id 'fabric-loom' version '1.8-SNAPSHOT'
	id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "GeckoLib"
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
	}
	maven {
		name = "BlameJared"
		url = "https://maven.blamejared.com/"
	}
	maven {
		name = "Ladysnake Mods"
		url = 'https://maven.ladysnake.org/releases/'
	}
	maven {
		name = "Shedaniel"
		url = "https://maven.shedaniel.me/"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		name = "Wisp Forest"
		url = "https://maven.wispforest.io/"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io/"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Libraries
	// https://github.com/emilyploszaj/trinkets
	modImplementation("dev.emi:trinkets:${project.trinkets_version}", {exclude group: "net.fabricmc.fabric-api"})

	// https://github.com/shedaniel/RoughlyEnoughItems
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}", {exclude group: "net.fabricmc.fabric-api"})
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}", {exclude group: "net.fabricmc.fabric-api"})
	// modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	// https://github.com/mezz/JustEnoughItems
	// modCompileOnly "mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}"
	// modLocalRuntime "mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}"

	// https://github.com/emilyploszaj/emi
	modCompileOnly "dev.emi:emi-fabric:${project.emi_version}:api"
	modLocalRuntime "dev.emi:emi-fabric:${project.emi_version}"

	// https://github.com/wisp-forest/owo-lib
	annotationProcessor("io.wispforest:owo-lib:${project.owo_version}", {exclude group: "net.fabricmc.fabric-api"})
	modImplementation("io.wispforest:owo-lib:${project.owo_version}", {exclude group: "net.fabricmc.fabric-api"})
	include "io.wispforest:owo-sentinel:${project.owo_version}"

	// https://github.com/Virtuoel/Pehkui
	modApi("com.github.Virtuoel:Pehkui:${pehkui_version}", {exclude group: "net.fabricmc.fabric-api"})

	// https://github.com/bernie-g/geckolib
	modImplementation "software.bernie.geckolib:geckolib-fabric-${project.minecraft_version}:${project.geckolib_version}"

	// https://github.com/LlamaLad7/MixinExtras
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}")))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	exclude("assets/arcadiandream/sounds/records/README.md")
}

loom {
	mods {
		"arcadiandream" {
			sourceSet sourceSets.main
		}
	}

	runs {
		datagen {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated/resources")}"
			vmArg "-Dfabric-api.datagen.modid=${mod_id}"

			runDir "build/datagen"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated/resources'
			]
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
	from("LICENSE.ASSETS") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// Configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
